"Name"     = 'Enter the name of the grammar'
"Author"   = 'Enter your name'
"Version"  = 'The version of the grammar and/or language'
"About"    = 'A short description of the grammar'
"Case Sensitive" = False
"Auto Whitespace" = False
"Start Symbol" = <Start>
"Virtual Terminals" = LexicalError String

! -------------------------------------------------
! Character Sets
! -------------------------------------------------

{WS} = {Whitespace} - {CR} - {LF} - {NBSP}
{TokenSeparator} = {HT}

! -------------------------------------------------
! Terminals
! -------------------------------------------------

! The following defines the Whitespace terminal using the {WS}
! set - which excludes the carriage return and line feed
! characters

Whitespace    = {WS}+
NewLine       = {CR}{LF} | {CR} | {LF}
Integer       = {Number}+
S             = {TokenSeparator}
Q             = '"'

Q @= { Type = Noise }

! -------------------------------------------------
! Rules
! -------------------------------------------------

<Plus Opt> ::= '+' | <>
<Comma Opt> ::= ',' | <>

<Special Attack Type> ::= 'curse'
                       |  'weak'
                       |  'asleep'
                       |  'drunk'
                       |  'insane'
                       |  'poison1' | 'poison weak'
                       |  'poison2' | 'poison medium'
                       |  'poison3' | 'poison severe'
                       |  'disease1' | 'disease weak'
                       |  'disease2' | 'disease medium'
                       |  'disease3' | 'disease severe'
                       |  'paralyze'
                       |  'uncon'
                       |  'dead'
                       |  'stone'
                       |  'errad'
                       |  'brkitem'
                       |  'brkarmor'
                       |  'brkweapon'
                       |  'steal'
                       |  'age'
                       |  'drainsp'
                       |  'afraid'

<Special Attack> ::= <Special Attack Type> <Comma Opt> 'x' Integer
                  |  <Special Attack Type>
                  |  Integer

<Treasure Type> ::= 'weapon'
                 |  'armor'
                 |  'misc'
                 |  'sword'
                 |  'dagger'
                 |  'axe'
                 |  'spear'
                 |  'bow'
                 |  'mace'
                 |  'club'
                 |  'staff'
                 |  'leather'
                 |  'chain'
                 |  'plate'
                 |  'shield'
                 |  'helm'
                 |  'belt'
                 |  'cape'
                 |  'gauntlets'
                 |  'boots'
                 |  'ring'
                 |  'amulet'
                 |  'wand'
                 |  'scroll'
                 |  'gem'

<Treasure Chance> ::= Integer '%'

<Treasure Dice> ::= Integer 'd' Integer

<Treasure Item> ::= 'l' Integer <Treasure Type>
                 |  'l' Integer

<Treasure Info> ::= <Treasure Chance> <Treasure Dice> <Plus Opt> <Treasure Item>
                 |  <Treasure Chance> <Treasure Dice>
                 |  <Treasure Chance> <Plus Opt> <Treasure Item>
                 |  <Treasure Dice> <Plus Opt> <Treasure Item>
                 |  <Treasure Dice>
                 |  <Treasure Item>
                 |  Integer

<Movement Type> ::= 'stand'
                 |  'short'
                 |  'med'
                 |  'long'
                 |  'global'
                 |  'free'

<AI Type> ::= 'suicidal'
           |  'wimp'
           |  'normal'
           |  'aggress'

<Damage Type> ::= 'fire'
               |  'air'
               |  'water'
               |  'earth'
               |  'phys' | 'physical'
               |  'light'
               |  'dark'
               |  'cold'
               |  'ener' | 'energy'
               |  Integer

<Damage Dice> ::= Integer 'd' Integer '+' Integer
               |  Integer 'd' Integer
               |  Integer

<Missile Type> ::= 'fire'
                |  'firear'
                |  'air'
                |  'water'
                |  'earth'
                |  'light'
                |  'dark'
                |  'arrow'
                |  'ener' | 'energy'
                |  Integer

<Spell> ::= 'acid burst'
         |  'blades'
         |  'bless'
         |  'day of protection'
         |  'dispel magic'
         |  'dragon breath'
         |  'fate'
         |  'fire bolt'
         |  'fireball'
         |  'hammerhands'
         |  'harm'
         |  'haste'
         |  'heroism'
         |  'hour of power'
         |  'ice blast'
         |  'ice bolt'
         |  'incinerate'
         |  'light bolt'
         |  'lightning bolt'
         |  'meteor shower'
         |  'mind blast'
         |  'pain reflection'
         |  'power cure'
         |  'psychic shock'
         |  'rock blast'
         |  'shield'
         |  'shrapmetal'
         |  'sparks'
         |  'stone skin'
         |  'toxic cloud'

<Skill Mastery> ::= 'n' | 'e' | 'm' | 'g'

<Spell Attack> ::= <Spell> ',' <Skill Mastery> <Comma Opt> Integer
                |  Integer

<Boolean> ::= 'y' | 'n' | Integer

<Number> ::= Integer | Integer ',' Integer
<Resistance> ::= Integer | 'imm'

<Attack Desc> ::= <Damage Type> S <Damage Dice> S <Missile Type>
<Spell Attack Desc> ::= Integer S <Spell Attack>
<Resistances> ::= <Resistance> S <Resistance> S <Resistance> S <Resistance> S <Resistance> S <Resistance> S <Resistance> S <Resistance> S <Resistance> S <Resistance>

<Misc Summon Spawn> ::= 'air' | 'ground'

<String Placeholder> ::= <>
<String> ::= <String Placeholder> LexicalError
          |  String

<Attack Preference> ::= <String>
                     |  Integer

<Misc Special> ::= 'explode' ',' <Damage Dice> ',' <Damage Type>
                |  'shot' ',' 'x' Integer
                |  'summon' ',' <Misc Summon Spawn> ',' <String>
                |  Integer

! The following rule defines a series of newline tokens.
! Use this rule rather than the newline terminal.

<nl> ::= NewLine <nl> !One or more
      |  NewLine

<nl Opt> ::= NewLine <nl Opt> !Zero or more
          |  <>

! <nl opt> removes blank lines before first statement

<Start>   ::= <Statement List> <nl Opt>

<Statement> ::= Integer S <String> S <String> S Integer S <Number> S Integer S <Number> S <Treasure Info> S <Boolean> S <Boolean> S <Movement Type> S <AI Type> S Integer S Integer S Integer S <Attack Preference> S <Special Attack> S <Attack Desc> S Integer S <Attack Desc> S <Spell Attack Desc> S <Spell Attack Desc> S <Resistances> S <Misc Special>

<Statement List> ::= <Statement List> <nl> <Statement>
                  |  <Statement>
                  |  <>
